plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'com.gorylenko.gradle-git-properties' version '2.0.0'
}

apply plugin: 'java'
apply plugin: 'idea'

group = 'marshmallow'
version = '0.1.1'
description = 'Marshmallow Discord Bot'
mainClassName = 'marshmallow.Main'

sourceCompatibility = 1.8

ext {
    moduleName = 'AvaIre'
}

shadowJar {
    archiveName = "Marshmallow.jar"

    doLast {
        copy {
            from 'build/libs/Marshmallow.jar'
            into '.'
        }
    }
}

tasks.withType(JavaCompile) {
    dependsOn 'clean'
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:-unchecked"
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Marshmallow: Discord Bot',
                'Implementation-Version': version,
                'Main-Class': 'marshmallow.Main'
    }

    baseName = project.name

    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    failOnError = false
    options.memberLevel = JavadocMemberLevel.PUBLIC
    options.author()
    options.encoding = 'UTF-8'
    options.addStringOption('-html5')
}

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven { url "https://dl.bintray.com/kodehawa/maven" }
    maven { url 'https://dl.bintray.com/natanbc/maven' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    // Main dependencies
    compile 'net.dv8tion:JDA:3.8.3_463'
    compile group: 'com.sedmelluq', name: 'lavaplayer', version: '1.3.17'
    compile group: 'com.sedmelluq', name: 'jda-nas', version: '1.0.6'

    // cmdline and text manipulation
    compile group: 'commons-cli', name: 'commons-cli', version: '1.3.1'
    compile group: 'org.apache.commons', name: 'commons-text', version: '1.1'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.1'
    compile group: 'org.yaml', name: 'snakeyaml', version: '1.19'
    compile group: 'org.jsoup', name: 'jsoup', version: '1.11.2'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.1'
    compile group: 'ai.api', name: 'libai', version: '1.6.12'

    compile group: 'com.udojava', name: 'EvalEx', version: '1.18'
    compile group: 'com.vdurmont', name: 'emoji-java', version: '4.0.0'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    compile group: 'io.sentry', name: 'sentry-logback', version: '1.7.10'
    compile group: 'org.reflections', name: 'reflections', version: '0.9.11'

    // database
    compile 'org.mongodb:mongo-java-driver:2.12.3'

    // lombok
    compileOnly 'org.projectlombok:lombok:1.18.8'
    annotationProcessor 'org.projectlombok:lombok:1.18.8'

    // tests
    compile("org.junit.platform:junit-platform-launcher:1.1.0")
    compile("org.junit.jupiter:junit-jupiter-engine:5.1.0")
    compile("org.junit.vintage:junit-vintage-engine:5.1.0")
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    filesMatching("**/app.properties") {
        filter ReplaceTokens, tokens: [
                "project.version"   : project.version,
                "project.groupId"   : project.group,
                "project.artifactId": project.ext.moduleName
        ]
    }
}